---
title: "Excelã®LAMBDAé–¢æ•°ã§æ•°å¼æ–‡å­—åˆ—ã‚’è¨ˆç®—ã™ã‚‹"
emoji: "ğŸš‚"
type: "tech" # tech: æŠ€è¡“è¨˜äº‹ / idea: ã‚¢ã‚¤ãƒ‡ã‚¢
topics: [excel, é€†ãƒãƒ¼ãƒ©ãƒ³ãƒ‰è¨˜æ³•]
published: true
---

# æ•°å¼æ–‡å­—åˆ—ã‚’è¨ˆç®—ã™ã‚‹Excelé–¢æ•°ã‚’ä½œã‚Šã¾ã—ãŸ

`1+2*(3+4)`ã®ã‚ˆã†ãªæ–‡å­—åˆ—ã‚’è§£é‡ˆã—ã¦è¨ˆç®—çµæœã‚’è¿”ã™é–¢æ•°ã‚’å®šç¾©ã™ã‚‹æ•°å¼ã‚’ä½œã‚Šã¾ã—ãŸã€‚ä»¥ä¸‹ã®æ•°å¼ã‚’Excelã®ã€Œåå‰ã®ç®¡ç†ã€ã§ç™»éŒ²ã™ã‚‹ã¨ä½¿ãˆã‚‹ã‚ˆã†ã«ãªã‚Šã¾ã™ã€‚

:::details æ•°å¼ï¼ˆé•·ã„ã®ã§æŠ˜ã‚Šç•³ã¿ï¼‰
```
=LET(
    is_num,LAMBDA(v,OR(v=".",ISNUMBER(1*v))),
    is_func_name_char,LAMBDA(v,ISEVEN(IFERROR(XMATCH(CODE(v),{64,90,96,122},1),1))),
    is_func_char,LAMBDA(v,ISEVEN(IFERROR(XMATCH(UNICODE(v),{47,57,64,90,94,95,96,122},1),1))),
    match_char_list,LAMBDA(v,list,NOT(ISNA(XMATCH(v,list)))),

    tokenize_pre,LAMBDA(str,REDUCE("",SEQUENCE(LEN(str)),LAMBDA(p,i,LET(
        c,MID(str,i,1),
        prev,TAKE(p,,-1),
        remain,DROP(p,,-1),
        IFS(
            match_char_list(c,{"+","ï¼‹"}),HSTACK(p,"+"),
            match_char_list(c,{"-"}),IF(OR(is_num(prev),prev=")"),HSTACK(p,"-"),HSTACK(p,"nega"," ")),
            match_char_list(c,{"*","Ã—"}),HSTACK(p,"*"),
            match_char_list(c,{"/","Ã·","ï¼"}),HSTACK(p,"/"),
            match_char_list(c,{"^"}),HSTACK(p,"^"),
            match_char_list(c,{"âˆš"}),HSTACK(p,"sqrt"," "),
            match_char_list(c,{"Ï€"}),HSTACK(p,"pi"),
            AND(is_func_char(c),is_func_name_char(prev)),HSTACK(remain,prev&c),
            AND(is_num(c),is_num(prev)),HSTACK(remain,prev&c),
            TRUE,HSTACK(p,c)
        )
    )))),
    remove_spaces,LAMBDA(str,SUBSTITUTE(str," ","")),
    tokenize,LAMBDA(arr,DROP(MAP(tokenize_pre(remove_spaces(arr)),LAMBDA(v,IF(is_num(v),1*v,v))),,1)),

    operators,{"+";"-";"*";"/";"^";"âˆš";"nega";"min";"max";"pi";"(";")";",";" "},
    operator_priority_table,{2,"lo";2,"lo";3,"lo";3,"lo";4,"lo";0,"fn";5,"fn";0,"fn";0,"fn";6,"fn";2,"lp";2,"rp";2,"sp";0,"br"},

    get_priority,LAMBDA(t,INDEX(XLOOKUP(t,operators,operator_priority_table,0),1,1)),
    get_direction,LAMBDA(t,IF(ISNUMBER(t),"no",INDEX(XLOOKUP(t,operators,operator_priority_table,{"",""}),1,2))),
    h_reverse,LAMBDA(arr,LET(cols,COLUMNS(arr),MAKEARRAY(ROWS(arr),cols,LAMBDA(r,c,INDEX(arr,r,cols-c+1))))),
    pick_output_stack,LAMBDA(v,LET(w,INDEX(v,1),FILTER(w,NOT(ISNA(w))))),
    pick_operator_stack,LAMBDA(v,LET(w,INDEX(v,2),FILTER(w,NOT(ISNA(w))))),
    reverse_and_join,LAMBDA(a,b,HSTACK(a,h_reverse(b))),

    shunting_yard,LAMBDA(tokens,REDUCE({0;0},tokens,LAMBDA(p,c,LET(
        output,pick_output_stack(p),
        operator,pick_operator_stack(p),

        SWITCH(get_direction(c),
            "no",VSTACK(HSTACK(output,c),operator),
            "fn",VSTACK(output,HSTACK(operator,c)),
            "sp",LET(
                pos,XMATCH("(",operator,,-1),
                o,reverse_and_join(output,DROP(operator,,pos)),
                s,TAKE(operator,,pos),
                VSTACK(o,s)
            ),
            "lo",LET(
                pr_o1,get_priority(c),
                farr,MAP(operator,LAMBDA(v,AND(get_priority(v)>=pr_o1,OR(get_direction(v)="lo",get_direction(v)="ro")))),
                pos,XMATCH(FALSE,farr,,-1),
                o,IF(OR(pos<2,COLUMNS(operator)=pos),output,reverse_and_join(output,DROP(operator,,pos))),
                s,HSTACK(TAKE(operator,,pos),c),
                VSTACK(o,s)
            ),
            "ro",LET(
                pr_o1,get_priority(c),
                farr,MAP(operator,LAMBDA(v,prior(v)>pr_o1)),
                pos,XMATCH(FALSE,farr,,-1),
                o,IF(OR(pos<2,COLUMNS(operator)=pos),output,reverse_and_join(output,DROP(operator,,pos))),
                s,HSTACK(TAKE(operator,,pos),c),
                VSTACK(o,s)
            ),
            "lp",VSTACK(output,HSTACK(operator,c)),
            "rp",LET(
                pos,XMATCH("(",operator,,-1),
                is_func,get_direction(INDEX(operator,pos-1))="fn",
                oo,IF(pos=COLUMNS(operator),output,reverse_and_join(output,DROP(operator,,pos))),
                o,IF(is_func,HSTACK(oo,INDEX(operator,pos-1)),oo),
                s,TAKE(operator,,pos-IF(is_func,2,1)),
                VSTACK(o,s)
            ),
            "br",p
        )
    )))),

    convert_to_rpn,LAMBDA(tokens,LET(
        output_and_stack,shunting_yard(tokens),
        output,pick_output_stack(output_and_stack),
        stack,pick_operator_stack(output_and_stack),
        reverse_and_join(output,stack)
    )),

    eval_rpn,LAMBDA(rpn,REDUCE(,rpn,LAMBDA(stack,c,
        SWITCH(c,
            "+",LET(pop,TAKE(stack,,-2),remain,DROP(stack,,-2),ans,SUM(pop),HSTACK(remain,ans)),
            "-",LET(pop,TAKE(stack,,-2),remain,DROP(stack,,-2),ans,INDEX(pop,1)-INDEX(pop,2),HSTACK(remain,ans)),
            "*",LET(pop,TAKE(stack,,-2),remain,DROP(stack,,-2),ans,PRODUCT(pop),HSTACK(remain,ans)),
            "/",LET(pop,TAKE(stack,,-2),remain,DROP(stack,,-2),ans,INDEX(pop,1)/INDEX(pop,2),HSTACK(remain,ans)),
            "^",LET(pop,TAKE(stack,,-2),remain,DROP(stack,,-2),ans,INDEX(pop,1)^INDEX(pop,2),HSTACK(remain,ans)),
            "sqrt",LET(pop,TAKE(stack,,-1),remain,DROP(stack,,-1),ans,SQRT(pop),HSTACK(remain,ans)),
            "nega",LET(pop,TAKE(stack,,-1),remain,DROP(stack,,-1),ans,-1*pop,HSTACK(remain,ans)),
            "min",LET(pop,TAKE(stack,,-2),remain,DROP(stack,,-2),ans,MIN(pop),HSTACK(remain,ans)),
            "max",LET(pop,TAKE(stack,,-2),remain,DROP(stack,,-2),ans,MAX(pop),HSTACK(remain,ans)),
            "pi",HSTACK(stack,PI()),
            HSTACK(stack,c)
        )
    ))),

    calculate,LAMBDA(str,LET(
        tokens,tokenize(str),
        rpn,convert_to_rpn(tokens),
        result,eval_rpn(rpn),
        SUM(result)
    )),
    calculate
)
```

ã‚ã£ã¡ã‚ƒé•·ã„ã£ã¦æ€ã„ã¾ã—ãŸã‚ˆã­ã€‚ç§ã‚‚æ€ã„ã¾ã—ãŸã€‚
:::

# æ¦‚è¦

ã“ã®æ•°å¼ã¯ã„ã¡ã©é€†ãƒãƒ¼ãƒ©ãƒ³ãƒ‰è¨˜æ³•ï¼ˆRPNï¼‰ã«å¤‰æ›ã™ã‚‹ã“ã¨ã§è¨ˆç®—å‡¦ç†ã‚’è¡Œã£ã¦ã„ã¾ã™[^1]ã€‚

[^1]: å†å¸°ä¸‹é™æ§‹æ–‡è§£æã§ã‚„ã‚ã†ã¨æ€ã£ãŸã‚‰ã™ãã‚¹ã‚¿ãƒƒã‚¯ã‚ªãƒ¼ãƒãƒ¼ãƒ•ãƒ­ãƒ¼ã«ãªã£ã¦ã—ã¾ã£ã¦ãƒ€ãƒ¡ã§ã—ãŸã€‚

```mermaid
graph TB
    A[æ™®é€šã®æ•°å¼æ–‡å­—åˆ—] -->|å­—å¥è§£æå™¨| B[ãƒˆãƒ¼ã‚¯ãƒ³é…åˆ—]
    B -->|é€†ãƒãƒ¼ãƒ©ãƒ³ãƒ‰è¨˜æ³•å¤‰æ›å™¨| C[é€†ãƒãƒ¼ãƒ©ãƒ³ãƒ‰è¨˜æ³•<br>ï¼ˆRPNï¼‰]
    C -->|RPNè©•ä¾¡å™¨| D[è¨ˆç®—çµæœ]
```

# ã‚µãƒãƒ¼ãƒˆã™ã‚‹æ¼”ç®—å­ãƒ»è¨˜å·ãƒ»é–¢æ•°

æ•°å¼è©•ä¾¡ã‚’ã™ã‚‹ã¨ãã®å¸¸ã§ã™ãŒã€æœ€åˆã«æ¼”ç®—å­ã®å„ªå…ˆé †ä½ã¨çµåˆæ–¹å‘ã‚’å®šç¾©ã—ã¦ãŠã‹ãªã‘ã‚Œã°ãªã‚Šã¾ã›ã‚“ã€‚

ä»Šå›ã¯å››å‰‡æ¼”ç®—ãƒ»ã¹ãä¹—ãƒ»ã‚«ãƒƒã‚³ãƒ»ã®ã»ã‹ã«ã„ãã¤ã‹ã®é–¢æ•°ã«å¯¾å¿œã•ã›ã¾ã™ã€‚ãªãŠã€ã¹ãä¹—ã¯Excelã®ä»•æ§˜ã«åˆã‚ã›ã¦å·¦çµåˆã¨ã—ã¦ã„ã¾ã™ã€‚
è² å€¤ã®è¨˜å·`-`ã¨å®šæ•°`Ï€`ã¯é–¢æ•°ã¨ã—ã¦å®šç¾©ã—ã¾ã™ã€‚

| æ¼”ç®—å­               | å„ªå…ˆé †ä½ | çµåˆæ–¹å‘   | è­˜åˆ¥å­                 | è£œè¶³                                      |
| -------------------- | -------: | ---------- | ---------------------- | ----------------------------------------- |
| +                    | 2        | å·¦         | lo (left operator)     | `ï¼‹`ã«ã‚‚å¯¾å¿œã•ã›ã‚‹                        |
| -                    | 2        | å·¦         | lo                     | è² å€¤ã®è¨˜å·ã¯åˆ¥ã«å®šç¾©ã™ã‚‹                  |
| *                    | 3        | å·¦         | lo                     | `Ã—`ã€`ãƒ»`ã«ã‚‚å¯¾å¿œã•ã›ã‚‹                  |
| /                    | 3        | å·¦         | lo                     | `Ã·`ã€`ï¼`ã«ã‚‚å¯¾å¿œã•ã›ã‚‹                  |
| ^                    | 4        | å·¦         | lo                     | ä¸€èˆ¬çš„ã«ã¯å³çµåˆã ãŒExcelã®ä»•æ§˜ã«åˆã‚ã›ã‚‹ |
| âˆš                   | 0        | é–¢æ•°       | fn (function)          | `sqrt`                                    |
| -                    | 5        | é–¢æ•°       | fn                     | ï¼ˆè² å€¤ã®è¨˜å·ï¼‰ `nega`                     |
| min                  | 0        | é–¢æ•°       | fn                     | `min` å¼•æ•°ã‚’2ã¤å–ã‚‹é–¢æ•°ã¨ã™ã‚‹             |
| max                  | 0        | é–¢æ•°       | fn                     | `max` å¼•æ•°ã‚’2ã¤å–ã‚‹é–¢æ•°ã¨ã™ã‚‹             |
| Ï€                   | 6        | é–¢æ•°       | fn                     | å®šæ•°ã¯å¼•æ•°0ã®é–¢æ•°æ‰±ã„ã¨ã™ã‚‹[^2]           |
| (                    | 2        | å·¦ã‚«ãƒƒã‚³   | lp (left parenthesis)  |                                           |
| )                    | 2        | å³ã‚«ãƒƒã‚³   | rp (right parenthesis) |                                           |
| ,                    | 2        | ã‚»ãƒ‘ãƒ¬ãƒ¼ã‚¿ | sp (separator)         | é–¢æ•°ã®å¼•æ•°ã‚’åŒºåˆ‡ã‚‹è¨˜å·                    |
| [0-9]+(\.([0-9]+)?)? | 0        | æ•°å€¤       | no (number)            |                                           |

[^2]: é–¢æ•°æ‰±ã„ã§ä¸éƒ½åˆã¯ç”Ÿã˜ãªã„ã‚ˆã­ï¼Ÿ

# é–¢æ•°ã‚’çµ„ã¿ç«‹ã¦ã‚‹

`LET`é–¢æ•°ã®ä¸­ã§`tokenize`ï¼ˆå­—å¥è§£æå™¨ï¼‰ã€`convert_to_rpn`ï¼ˆé€†ãƒãƒ¼ãƒ©ãƒ³ãƒ‰è¨˜æ³•å¤‰æ›å™¨ï¼‰ã€`eval_rpn`ï¼ˆRPNè©•ä¾¡å™¨ï¼‰ã‚’å®šç¾©ã—ã€ãã‚Œã‚‰ã‚’çµåˆã—ãŸ`calculate`é–¢æ•°ã‚’è¿”ã—ã¦ã„ã¾ã™ã€‚
ã“ã®æ§‹æˆã§ä½œã‚‹ã¨ãã‚Œãã‚Œã®LAMBDAé–¢æ•°ã”ã¨ã®ãƒ‡ãƒãƒƒã‚°ãŒã—ã‚„ã™ã„ã¨æ€ã„ã¾ã™ã€‚

```
=LET(
    tokenize,LAMBDA(v,???),
    convert_to_rpn,LAMBDA(v,???),
    eval_rpn,LAMBDA(v,???),
    calculate,LAMBDA(str,let(
        tokens,tokenize(str),
        rpn,convert_to_rpn(tokens),
        result,eval_rpn(rpn),
        SUM(result)
    )),
    calculate
)
```

## å­—å¥è§£æå™¨

å­—å¥è§£æå™¨ã¯ã“ã®éƒ¨åˆ†ã§ã™ã€‚

```
    is_num,LAMBDA(v,OR(v=".",ISNUMBER(1*v))),
    is_func_name_char,LAMBDA(v,ISEVEN(IFERROR(XMATCH(CODE(v),{64,90,96,122},1),1))),
    is_func_char,LAMBDA(v,ISEVEN(IFERROR(XMATCH(UNICODE(v),{47,57,64,90,94,95,96,122},1),1))),
    match_char_list,LAMBDA(v,list,NOT(ISNA(XMATCH(v,list)))),

    tokenize_pre,LAMBDA(str,REDUCE("",SEQUENCE(LEN(str)),LAMBDA(p,i,LET(
        c,MID(str,i,1),
        prev,TAKE(p,,-1),
        remain,DROP(p,,-1),
        IFS(
            match_char_list(c,{"+","ï¼‹"}),HSTACK(p,"+"),
            match_char_list(c,{"-"}),IF(OR(is_num(prev),prev=")"),HSTACK(p,"-"),HSTACK(p,"nega"," ")),
            match_char_list(c,{"*","Ã—"}),HSTACK(p,"*"),
            match_char_list(c,{"/","Ã·","ï¼"}),HSTACK(p,"/"),
            match_char_list(c,{"^"}),HSTACK(p,"^"),
            match_char_list(c,{"âˆš"}),HSTACK(p,"sqrt"," "),
            match_char_list(c,{"Ï€"}),HSTACK(p,"pi"),
            AND(is_func_char(c),is_func_name_char(prev)),HSTACK(remain,prev&c),
            AND(is_num(c),is_num(prev)),HSTACK(remain,prev&c),
            TRUE,HSTACK(p,c)
        )
    )))),
    remove_spaces,LAMBDA(str,SUBSTITUTE(str," ","")),
    tokenize,LAMBDA(arr,DROP(MAP(tokenize_pre(remove_spaces(arr)),LAMBDA(v,IF(is_num(v),1*v,v))),,1)),
```

1. `remove_spaces`é–¢æ•°ã§ã€æ–‡å­—åˆ—ã‹ã‚‰ç©ºç™½ã‚’å–ã‚Šé™¤ã
1. `tokenize_pre`é–¢æ•°ã§ã€1æ–‡å­—ãšã¤èª­ã‚“ã§ä»¥ä¸‹ã®ã‚ˆã†ã«é…åˆ—ã‚’çµ„ã¿ç«‹ã¦ã‚‹
   - èª­ã‚“ã§ã„ã‚‹æ–‡å­—`c`ãŒæ•°å€¤ã®ä¸€éƒ¨ã‹é–¢æ•°åã®ä¸€éƒ¨ã§ã‚ã‚Šã€ã‹ã¤é…åˆ—ã®æœ«å°¾`prev`ã¨åŒç¨®ãªã‚‰é…åˆ—ã®æœ«å°¾ã®æ–‡å­—åˆ—ã«çµåˆã™ã‚‹ï¼ˆpopã—ã¦joinã—ã¦pushã™ã‚‹ï¼‰
   - èª­ã‚“ã§ã„ã‚‹æ–‡å­—`c`ãŒæ•°å€¤ã®ä¸€éƒ¨ã‹é–¢æ•°åã®ä¸€éƒ¨ã§ã‚ã‚Šã€é…åˆ—ã®æœ«å°¾`prev`ã¨**åˆ¥ç¨®**ãªã‚‰é…åˆ—ã®æœ«å°¾ã«æ–°ã—ã„è¦ç´ ã¨ã—ã¦`c`ã‚’é€£çµã™ã‚‹ï¼ˆpushã™ã‚‹ï¼‰
   - ãã‚Œä»¥å¤–ãªã‚‰èª­ã‚“ã§ã„ã‚‹æ–‡å­—`c`ã«å¿œã˜ãŸè¨˜å·ã‚’é…åˆ—ã«é€£çµã™ã‚‹ï¼ˆpushã™ã‚‹ï¼‰
1. `tokenize`é–¢æ•°ã§ã€é…åˆ—ã®ä¸­ã®æ•°å€¤ã‚’è¡¨ã™æ–‡å­—åˆ—ã‚’æ•°å€¤ã«å¤‰æ›ã™ã‚‹

## é€†ãƒãƒ¼ãƒ©ãƒ³ãƒ‰è¨˜æ³•ï¼ˆRPNï¼‰å¤‰æ›å™¨

æœ€åˆã®2ã¤ã®é…åˆ—ã¯æ¼”ç®—å­ã®å„ªå…ˆé †ä½è¡¨ã‚’å®šç¾©ã—ãŸã‚‚ã®ã§ã™ã€‚`operators`ãŒæ¼”ç®—å­ã®ãƒªã‚¹ãƒˆã€`operator_priority_table`ãŒå„ªå…ˆé †ä½ã‚„çµåˆæ–¹å‘ã‚’å®šç¾©ã—ãŸãƒ†ãƒ¼ãƒ–ãƒ«ã§ã™ã€‚

```
    operators,{"+";"-";"*";"/";"^";"âˆš";"nega";"min";"max";"pi";"(";")";",";" "},
    operator_priority_table,{2,"lo";2,"lo";3,"lo";3,"lo";4,"lo";0,"fn";5,"fn";0,"fn";0,"fn";6,"fn";2,"lp";2,"rp";2,"sp";0,"br"},
```

ãã—ã¦ã€RPNå¤‰æ›å™¨ã¯ã“ã®éƒ¨åˆ†ã§ã™ã€‚

```
    get_priority,LAMBDA(t,INDEX(XLOOKUP(t,operators,operator_priority_table,0),1,1)),
    get_direction,LAMBDA(t,IF(ISNUMBER(t),"no",INDEX(XLOOKUP(t,operators,operator_priority_table,{"",""}),1,2))),
    h_reverse,LAMBDA(arr,LET(cols,COLUMNS(arr),MAKEARRAY(ROWS(arr),cols,LAMBDA(r,c,INDEX(arr,r,cols-c+1))))),
    pick_output_stack,LAMBDA(v,LET(w,INDEX(v,1),FILTER(w,NOT(ISNA(w))))),
    pick_operator_stack,LAMBDA(v,LET(w,INDEX(v,2),FILTER(w,NOT(ISNA(w))))),
    reverse_and_join,LAMBDA(a,b,HSTACK(a,h_reverse(b))),

    shunting_yard,LAMBDA(tokens,REDUCE({0;0},tokens,LAMBDA(p,c,LET(
        output,pick_output_stack(p),
        operator,pick_operator_stack(p),

        SWITCH(get_direction(c),
            "no",VSTACK(HSTACK(output,c),operator),
            "fn",VSTACK(output,HSTACK(operator,c)),
            "sp",LET(
                pos,XMATCH("(",operator,,-1),
                o,reverse_and_join(output,DROP(operator,,pos)),
                s,TAKE(operator,,pos),
                VSTACK(o,s)
            ),
            "lo",LET(
                pr_o1,get_priority(c),
                farr,MAP(operator,LAMBDA(v,AND(get_priority(v)>=pr_o1,OR(get_direction(v)="lo",get_direction(v)="ro")))),
                pos,XMATCH(FALSE,farr,,-1),
                o,IF(OR(pos<2,COLUMNS(operator)=pos),output,reverse_and_join(output,DROP(operator,,pos))),
                s,HSTACK(TAKE(operator,,pos),c),
                VSTACK(o,s)
            ),
            "ro",LET(
                pr_o1,get_priority(c),
                farr,MAP(operator,LAMBDA(v,prior(v)>pr_o1)),
                pos,XMATCH(FALSE,farr,,-1),
                o,IF(OR(pos<2,COLUMNS(operator)=pos),output,reverse_and_join(output,DROP(operator,,pos))),
                s,HSTACK(TAKE(operator,,pos),c),
                VSTACK(o,s)
            ),
            "lp",VSTACK(output,HSTACK(operator,c)),
            "rp",LET(
                pos,XMATCH("(",operator,,-1),
                is_func,get_direction(INDEX(operator,pos-1))="fn",
                oo,IF(pos=COLUMNS(operator),output,reverse_and_join(output,DROP(operator,,pos))),
                o,IF(is_func,HSTACK(oo,INDEX(operator,pos-1)),oo),
                s,TAKE(operator,,pos-IF(is_func,2,1)),
                VSTACK(o,s)
            ),
            "br",p
        )
    )))),

    convert_to_rpn,LAMBDA(tokens,LET(
        output_and_stack,shunting_yard(tokens),
        output,pick_output_stack(output_and_stack),
        stack,pick_operator_stack(output_and_stack),
        reverse_and_join(output,stack)
    )),
```

RPNå¤‰æ›å™¨ã¯æ“è»Šå ´ã‚¢ãƒ«ã‚´ãƒªã‚ºãƒ ã®å®Ÿè£…ã§ã™ã€‚æ“è»Šå ´ã‚¢ãƒ«ã‚´ãƒªã‚ºãƒ ã¯ã‚¹ã‚¿ãƒƒã‚¯æ“ä½œã§æ•°å¼ã‚’å¤‰æ›ã™ã‚‹ã‚¢ãƒ«ã‚´ãƒªã‚ºãƒ ã§ã€å†å¸°å‘¼ã³å‡ºã—ã®å¿…è¦ãŒã‚ã‚Šã¾ã›ã‚“ã€‚
å†å¸°ã«å¼±ã„Excelã®`LAMBDA`é–¢æ•°ã§ã‚‚ä½¿ãˆã¾ã™ã€‚

`shunting_yard`é–¢æ•°å†…ã®`REDUCE({0;0},`ã§ã€2è¡Œ1åˆ—ã®é…åˆ—ã¨ã—ã¦å‡ºåŠ›ã‚­ãƒ¥ãƒ¼ã¨æ¼”ç®—å­ã‚¹ã‚¿ãƒƒã‚¯ã‚’åˆæœŸåŒ–ã—ã¦ã„ã¾ã™ã€‚1è¡Œç›®ãŒå‡ºåŠ›ã‚­ãƒ¥ãƒ¼ã€2è¡Œç›®ãŒæ¼”ç®—å­ã‚¹ã‚¿ãƒƒã‚¯ã§ã™ã€‚
æ¼”ç®—å­ã‚¹ã‚¿ãƒƒã‚¯ã«0ã‚’å…¥ã‚Œã¦ãŠãã¨æ¼”ç®—å­ã®å„ªå…ˆé †ä½åˆ¤å®šãªã©ã§ç•ªå…µã¨ã—ã¦æ©Ÿèƒ½ã—ã¦ãã‚Œã¾ã™[^3]ã€‚

[^3]: å‡ºåŠ›ã‚­ãƒ¥ãƒ¼ã‚’0ã§åˆæœŸåŒ–ã—ã¦ã„ã‚‹ã®ã¯`""`ã¨ã™ã‚‹ã‚ˆã‚Šæ–‡å­—æ•°ãŒå°‘ãªã„ã‹ã‚‰ã ã£ãŸæ°—ãŒã—ã¾ã™ã€‚

`SWITCH`æ–‡ã§ã¯ãƒˆãƒ¼ã‚¯ãƒ³ã®ç¨®é¡ã«å¿œã˜ã¦å‡ºåŠ›ã‚­ãƒ¥ãƒ¼ã¨æ¼”ç®—å­ã‚¹ã‚¿ãƒƒã‚¯ã‚’æ›´æ–°ã—ã€å…¨ã¦ã®ãƒˆãƒ¼ã‚¯ãƒ³ã‚’å‡¦ç†ã—ãŸå¾Œã€`convert_to_rpn`ã§å‡ºåŠ›ã‚­ãƒ¥ãƒ¼ã¨æ¼”ç®—å­ã‚¹ã‚¿ãƒƒã‚¯ã‚’çµåˆã—ã¾ã™ã€‚ã“ã‚ŒãŒRPNã§ã®æ•°å¼ã«ãªã£ã¦ã„ã¾ã™ã€‚

ã‚¢ãƒ«ã‚´ãƒªã‚ºãƒ ã®è©³ç´°ã¯Wikipediaãªã©ã‚’ã”è¦§ãã ã•ã„ã€‚

https://ja.wikipedia.org/wiki/%E6%93%8D%E8%BB%8A%E5%A0%B4%E3%82%A2%E3%83%AB%E3%82%B4%E3%83%AA%E3%82%BA%E3%83%A0

## RPNè©•ä¾¡å™¨

ã„ã‚ˆã„ã‚ˆå¤§è©°ã‚ã€RPNè©•ä¾¡å™¨ã®å®Ÿè£…ã§ã™ã€‚

```
    eval_rpn,LAMBDA(rpn,REDUCE(,rpn,LAMBDA(stack,c,
        SWITCH(c,
            "+",LET(pop,TAKE(stack,,-2),remain,DROP(stack,,-2),ans,SUM(pop),HSTACK(remain,ans)),
            "-",LET(pop,TAKE(stack,,-2),remain,DROP(stack,,-2),ans,INDEX(pop,1)-INDEX(pop,2),HSTACK(remain,ans)),
            "*",LET(pop,TAKE(stack,,-2),remain,DROP(stack,,-2),ans,PRODUCT(pop),HSTACK(remain,ans)),
            "/",LET(pop,TAKE(stack,,-2),remain,DROP(stack,,-2),ans,INDEX(pop,1)/INDEX(pop,2),HSTACK(remain,ans)),
            "^",LET(pop,TAKE(stack,,-2),remain,DROP(stack,,-2),ans,INDEX(pop,1)^INDEX(pop,2),HSTACK(remain,ans)),
            "sqrt",LET(pop,TAKE(stack,,-1),remain,DROP(stack,,-1),ans,SQRT(pop),HSTACK(remain,ans)),
            "nega",LET(pop,TAKE(stack,,-1),remain,DROP(stack,,-1),ans,-1*pop,HSTACK(remain,ans)),
            "min",LET(pop,TAKE(stack,,-2),remain,DROP(stack,,-2),ans,MIN(pop),HSTACK(remain,ans)),
            "max",LET(pop,TAKE(stack,,-2),remain,DROP(stack,,-2),ans,MAX(pop),HSTACK(remain,ans)),
            "pi",HSTACK(stack,PI()),
            HSTACK(stack,c)
        )
    ))),
```

RPNè©•ä¾¡å™¨ã¯ä»¥ä¸‹ã®æ‰‹é †ã§RPNãƒˆãƒ¼ã‚¯ãƒ³é…åˆ—ã‚’è©•ä¾¡ã—ã€è¨ˆç®—çµæœã‚’è¿”ã—ã¾ã™ã€‚

1. RPNã®ãƒˆãƒ¼ã‚¯ãƒ³ã‚’å‰ã‹ã‚‰é †ç•ªã«å‡¦ç†ã™ã‚‹
   - ãƒˆãƒ¼ã‚¯ãƒ³ãŒæ•°å€¤ã®å ´åˆã¯ã‚¹ã‚¿ãƒƒã‚¯ã«ç©ã‚€ï¼ˆæ“è»Šå ´ã§ã‚‚ã‚„ã£ãŸ`REDUCE`é–¢æ•°ã§ã®æ“ä½œï¼‰
   - ãƒˆãƒ¼ã‚¯ãƒ³ãŒæ¼”ç®—å­ã®å ´åˆã¯æ¬¡ã®æ“ä½œã‚’è¡Œã†
     1. ã‚¹ã‚¿ãƒƒã‚¯ã‹ã‚‰å¿…è¦ãªæ•°ã®æ•°å€¤ã‚’popã™ã‚‹
     1. å–ã‚Šå‡ºã—ãŸæ•°å€¤ã«å¯¾ã—ã¦æ¼”ç®—ã‚’å®Ÿè¡Œã™ã‚‹
     1. æ¼”ç®—çµæœã‚’ã‚¹ã‚¿ãƒƒã‚¯ã«pushã™ã‚‹

## å…¨éƒ¨çµ„ã¿åˆã‚ã›ã¦å®Œæˆ

ã“ã“ã¾ã§ã§ä½œã£ãŸ`tokenize`é–¢æ•°ãƒ»`convert_to_rpn`é–¢æ•°ãƒ»`eval_rpn`é–¢æ•°ã‚’ç›´åˆ—ã«ã¤ãªã’ãŸã®ãŒæ•°å¼æ–‡å­—åˆ—ã‚’è¨ˆç®—ã™ã‚‹é–¢æ•°ã§ã™ï¼ˆå…¨æ–‡ã¯è¨˜äº‹å†’é ­ã®æŠ˜ã‚Šç•³ã¿ã‚’ã”è¦§ãã ã•ã„ï¼‰ã€‚é•·ã„å¼ã«ãªã‚Šã¾ã—ãŸãŒã€ã¤ã„ã«å®Œæˆã§ã™ã€‚

# è›‡è¶³

- ã“ã®æ•°å¼ã¯1å¹´ãã‚‰ã„å‰ã«ã»ã¨ã‚“ã©å®Œæˆã—ã¦ã„ãŸã‚“ã ã‘ã©ã€è¨˜äº‹ã‚’æ›¸ãã®ãŒã‚ã‚“ã©ãã•ãã¦æ”¾ç½®ã—ã¦ã¾ã—ãŸã€‚ãªã‚“ã¨ã‹æ›¸ã‘ã¦è‰¯ã‹ã£ãŸã€‚
- Excelã«ã‚‚æ­£è¦è¡¨ç¾é–¢æ•°ãŒã‚„ã£ã¦ããŸã®ã§ã€ãã‚Œã‚’ä½¿ãˆã°å­—å¥è§£æå™¨ã¯ã‚‚ã†ã¡ã‚‡ã£ã¨ã‚¹ãƒƒã‚­ãƒªæ›¸ã‘ãã†ã€‚
- å¼•æ•°ã®æ•°ãŒè‡ªç”±ãªé–¢æ•°ã‚’å®Ÿç¾ã™ã‚‹ãŸã‚ã®ãµã‚ã£ã¨ã—ãŸã‚¢ã‚¤ãƒ‡ã‚¢ã‚‚ã‚ã‚‹ã®ã§ã€æ™‚é–“ã®ã‚ã‚‹ã¨ãã«å®Ÿè£…ã—ãŸã„ã€‚
- å¤‰æ•°åã‚’ãƒ–ãƒ©ãƒƒã‚·ãƒ¥ã‚¢ãƒƒãƒ—ã™ã‚‹ãŸã‚ã«Geminiï¼ˆ2.0 Flash Thinking Experimentalï¼‰ã«ã“ã®æ•°å¼ã‚’ä¸¸æŠ•ã’ã—ãŸã‚‰ã€ã»ã¼å®Œãºãã«æ„å‘³ã‚’èª­ã¿å–ã£ã¦è§£èª¬ã—ã¦ãã‚ŒãŸã€‚**ã‚‚ã†ã“ã‚“ãªè¨˜äº‹æ›¸ã‹ãªãã¦ã‚‚ãŠå‰ã§ã„ã„ã˜ã‚ƒã‚“**ã€‚